apply plugin: 'com.android.application'

dependencies {
   implementation files('../../../lib/facerec.jar')
   implementation "androidx.core:core:1.0.0"
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.vdt.face_recognition.demo"
        minSdkVersion 14
        targetSdkVersion 29
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../../../lib"]
        }
    }

//Insert unique string into asest to avoid copy assets to data folder every time on load app.
//Checking this unique string in UnpackAssetsActivity
    task computeAssetsHash() {
        doLast {
            mkdir("$projectDir/src/main/assets/")
            new File(  "$projectDir/src/main/assets/", "assets-hash.txt").text = 
                """Buildtime: ${new Date().format("YYYY:MM:dd:HH:mm:ss", TimeZone.getTimeZone("GMT"))}"""
        }
    }

    task copyFiles(type: Copy) {
        description = 'Copy files'
        from("$projectDir/../../.."){
            include (
                "conf/**",
                "share/facedetectors/config_lbf/**",
                "share/fda/**",
                "share/mesh/**",
                "share/quality/**",
                "share/quality_iso/**",
                "share/face_quality/**",
                "share/faceanalysis/**",
                "share/age_second/**",
                "share/liveness_2d_v2/**",
                "share/faceattributes/**",
                "license/**"
            )
        }
        into "$projectDir/src/main/assets/"
    }

    preBuild.dependsOn computeAssetsHash, copyFiles
}
